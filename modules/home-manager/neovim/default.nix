{
  inputs,
  lib,
  config,
  pkgs,
  ...
}: let
  cfg = config.mod.programs.neovim;
  stylixCfg = config.theme;
  themes = import "${inputs.dotstylix}/themes" {inherit lib;};
  paletteAttrset = themes.${stylixCfg.scheme};
  paletteToLua = attrs:
    lib.concatStringsSep "\n" (
      lib.mapAttrsToList (name: value: "          ${name} = \"#${value}\",") attrs
    );
  colorschemeLua = ''
    --
    -- THIS FILE IS DYNAMICALLY GENERATED BY NIX
    --
    return {
      {
        "nvim-mini/mini.nvim",
        priority = 1000,
        config = function()
          require("mini.base16").setup({
            palette = {
              ${paletteToLua paletteAttrset}
            },
            use_cterm = false,
            plugins = { default = true }

          })
        end,
      },
    }
  '';
in {
  options.mod.programs.neovim = {
    enable = lib.mkEnableOption "Enable the neovim feature";
  };

  config = lib.mkIf cfg.enable {
    programs.neovim = {
      enable = true;
      defaultEditor = true;
      viAlias = true;
      vimAlias = true;
      vimdiffAlias = true;
      extraPackages = with pkgs; [
        gcc
        xclip
        xsel
        wl-clipboard
        ripgrep
        fd
        trash-cli
        imagemagick
        ghostscript
        mermaid-cli
        alejandra
        black
        google-java-format
        stylua
        nodePackages.prettier
        checkstyle
        eslint_d
        ktlint
        luajitPackages.luacheck
        ruff
        shellcheck
        statix
      ];
    };

    xdg.configFile."nvim" = {
      recursive = true;
      source = inputs.dotnvim;
    };

    xdg.configFile."nvim/lua/plugins/colorscheme.lua" = lib.mkIf stylixCfg.enable {
      text = colorschemeLua;
      onChange = ''
        rm -rf "${config.xdg.cacheHome}/nvim/"
      '';
    };
  };
}
